plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.4'
	id 'org.openapi.generator' version '7.8.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id "io.freefair.lombok" version "8.10.2"
}

group = 'com.spr'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
		targetCompatibility = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-graphql'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4'
	implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.22'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.4'
	testImplementation 'org.springframework:spring-webflux'
	testImplementation 'org.springframework.graphql:spring-graphql-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}
def commonSpringAdditionalProperties = [
		java17                 : "true",
		useSpringBoot3         : "true",
		interfaceOnly          : "true",
		openApiNullable        : "false",
		useOptional            : "true",
		hideGenerationTimestamp: "true",
		serializableModel      : "true",
		useJakartaEe           : "true",
		requestMappingMode     : "api_interface",
		generatedConstructorWithRequiredArgs: "false",
		removeEnumValuePrefix  : "false",
		additionalModelTypeAnnotations : ";@lombok.NoArgsConstructor;@lombok.AllArgsConstructor;",
		sourceFolder           : ".", // ← `src/main/java` を自動追加しない
]

openApiGenerate {
	generatorName = "spring"
	inputSpec = "$rootDir/openapi/query.yaml"
	outputDir = "$rootDir/src/main/java"

	apiPackage = 'com.spr.generated.controller'  // API層のパッケージ
	modelPackage = 'com.spr.generated.model'  // ドメインモデル層のパッケージ
	configOptions = commonSpringAdditionalProperties
}


sourceSets {
	main {
		java {
			srcDirs = ['src'] // src をソースディレクトリとして指定
		}
	}
}


tasks.named('test') {
	useJUnitPlatform()
}